#!/bin/zsh

# Improve error handling and script safety
set -o errexit -o pipefail -o noclobber -o nounset

# Parse options
zmodload zsh/zutil
zparseopts -D -E -A opts -F - h=help -help=help \
           l=list -list=list \
           -no-packages \
           -no-sync

if (( $#help )) ; then
	cat << EOT
Usage: $(basename "$0") [options]

Setup dotfiles and tool configurations

Options:
    -h, --help      Print this help text
    -l, --list      Print a list of additional software to install
    --no-packages   Suppress package updates
    --no-sync       Suppress pulling from the remote repository
EOT
  exit
fi

if ! type git > /dev/null ; then
	echo "git is required to continue"
	exit 1
fi

DOTFILES="${HOME}/.dotfiles"
DOTFILES_BACKUPS="${DOTFILES}/backup"
DOTFILES_GIT_REMOTE="git@github.com:KnBrckr/dotfiles.git"

# Clone a repo to target directory if it does not already exist
function clone {
	repo=$1
	dest=$2

	if git rev-parse --resolve-git-dir "$dest/.git" &> /dev/null ; then
		# Target is already cloned
		echo "\x1B[1;32m>>> Clone exists for $dest\x1B[0m"
		return 0
	fi

	if [ -e "$dest" ]; then
		echo "\x1B[1;31m$dest already exists, unable to clone\x1B[0m"
		return 1
	fi

	echo "\x1B[1;32m>>> Cloning $dest\x1B[0m"
	git clone "$repo" "$dest"
}

# Get the ball rolling with the main dotfiles content
clone "$DOTFILES_GIT_REMOTE" "$DOTFILES"

# Bootstrap XDG environment
source "$DOTFILES/shell/zshenv"
if [ -z "$XDG_CONFIG_HOME" ]; then
	printf "\x1B[1;31mXDG_CONFIG_HOME is not defined\x1B[0m"
	exit 1
fi
# Ensure config directory exists
mkdir -p "$XDG_CONFIG_HOME"

cd ${DOTFILES}

if [ ! -d backup ]; then
	mkdir backup
fi

source ./shell/functions/is_platform
source ./shell/functions/command_exists
source ./shell/functions/print_colors
source ./lib/help
source ./lib/list
source ./lib/utils
source ./lib/brew
source ./lib/pecl
source ./lib/npm
source ./lib/mirror_files

# List additional software to install
if (( $#list )) ; then
	run_list
	exit 0
fi

# Load Oh My Zsh environment
if [ ! -d ~/.oh-my-zsh ]; then
	e_header "Installing Oh My Zsh!"
	sh -c "$(curl -fsSL https://raw.github.com/ohmyzsh/ohmyzsh/master/tools/install.sh)" "" --unattended
fi

# Clone extra content
#
# clone git@github.com:alacritty/alacritty-theme.git $XDG_CONFIG_HOME/alacritty-theme
# clone https://github.com/seebi/dircolors-solarized.git "${DOTFILES}/submodules/dircolors-solarized"
clone https://github.com/zsh-users/zsh-syntax-highlighting.git "$DOTFILES/shell/zshcustom/plugins/zsh-syntax-highlighting"
clone https://github.com/zsh-users/zsh-autosuggestions.git "$DOTFILES/shell/zshcustom/plugins/zsh-autosuggestions"
clone https://github.com/djui/alias-tips "$DOTFILES/shell/zshcustom/plugins/alias-tips"
clone https://github.com/romkatv/powerlevel10k.git "$DOTFILES/shell/zshcustom/themes/powerlevel10k"

if command_exists nvim ; then
	clone ssh://git@codeberg.org/sigstop/config-nvim.git "$XDG_CONFIG_HOME/nvim"
fi

if is_mac; then
	# Before relying on Brew, check that packages can be compiled
	if ! command_exists 'gcc'; then
		e_error "The XCode Command Line Tools must be installed first."
		if ! command_exists xcode-select; then
			printf "  Install Apple's Xcode environment from the app store"
			printf "  Then run: bash ~/.dotfiles/bin/dotfiles\n"
			exit 1
		fi
		e_header "Installing xcode command line tools"
		e_header "Restart dotfiles after xcode install is complete."
		xcode-select --install
		exit 1
	fi

	if ! gcc --version > /dev/null 2>&1 ; then
		e_header "Can't use gcc, trying sudo xcodebuild -license"
		if ! sudo xcodebuild -license; then
			e_error "Failed xcode setup; attempting to install CLI tools"
			e_header "Restart dotfiles after xcode install is complete."
			xcode-select --install
			exit 1
		fi
	fi

	# Check for Homebrew
	if ! command_exists 'brew'; then
		e_header "Installing Homebrew..."
		/bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
		eval "$(/opt/homebrew/bin/brew shellenv)"
	fi

	# Setup Launch Agents
	# enable_launch_agent com.pumastudios.keep-drives-spinning
fi

# Conditionally sync with the remote repository
if (( $+opts[--no-sync] )); then
	printf "Skipped dotfiles sync.\n"
else
	e_header "Syncing dotfiles..."
	# Pull down the latest changes
	git fetch origin

	# If directory is updated, dotfiles script itself may have changed.
	# In such a case, exit after updating local files, command must be
	# restarted
	
	LOCAL_REF=$(git rev-parse @)
	UPSTREAM_REF=$(git rev-parse @{u})
	BASE_REF=$(git merge-base @ @{u})
	
	RESTART=0
	if [ $LOCAL_REF = $UPSTREAM_REF ]; then
		e_info "Dotfiles up to date"
	elif [ $LOCAL_REF = $BASE_REF ]; then
		# Update local branch
		git pull
		RESTART=1
	elif [ $UPSTREAM_REF = $BASE_REF ]; then
		e_info "Push of local changes needed"
	else
		e_warning "Dotfiles has diverged ... trying rebase"
		git pull --rebase origin master
		RESTART=1
	fi

	if [ $RESTART -eq 1 ]; then
		e_info "Local files updated, restart dotfiles!"
		exit 1
	fi
fi

# Install and update packages
if (( $+opts[--no-packages] )) ; then
	e_header "Skipping package installs."
else
	e_header "Updating packages..."

	# Install Homebrew formulae
	run_brew

	# Install PECL packages
	run_pecl

	# Install npm packages
	run_npm
fi

if command -v pyenv > /dev/null 2>&1 ; then
	e_header "Python version active:"
	pyenv version
	echo "Use 'pyenv install <version> ; pyenv global <version>;' to update python version" 
fi

# Install tmux plugin manager
if [ ! -d ~/.tmux/plugins/tpm ]; then
	e_header "Installing tmux plugin manager"
	"${DOTFILES}/bin/install_tmux_plugin_mgr"
fi

# Ask before potentially overwriting files
e_header "Mirroring dotfiles..."
seek_confirmation "Warning: This step may overwrite your existing dotfiles."

if is_confirmed; then
	mirror_files
else
	printf "Skipped mirroring of dot files.\n"
fi

if is_mac; then
	# Ask before potentially overwriting OS X defaults
	e_header "Setting OSX Defaults..."
	seek_confirmation "Warning: This step may modify your OS X system defaults."

	if is_confirmed; then
		bash ./bin/osxdefaults
		e_success "OS X settings updated! You may need to restart."
	else
		printf "Skipped OS X settings update.\n"
	fi
fi
